buildscript {
  repositories {
    mavenCentral()
    gradlePluginPortal()
  }
  dependencies {
    classpath "io.ratpack:ratpack-gradle:1.8.0"
    classpath "com.github.jengelman.gradle.plugins:shadow:5.2.0"
    classpath "com.netflix.nebula:nebula-release-plugin:15.3.1"
  }
}

apply plugin: "io.ratpack.ratpack-java"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "idea"
apply plugin: "groovy"
apply plugin: "nebula.release"

repositories {
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

configurations {
  funTestCompile.extendsFrom testCompile
  funTestRuntime.extendsFrom testRuntime
}

dependencies {
  runtimeOnly "org.slf4j:slf4j-simple:1.7.12"

  implementation ratpack.dependency("guice")
  implementation "org.mongodb:mongo-java-driver:3.2.2"
  implementation "org.slf4j:slf4j-simple:1.7.5"
  implementation "com.github.sdkman:sdkman-persistent-model:1.1.1"

  testImplementation "org.codehaus.groovy:groovy-all:2.4.19"
  testImplementation "org.spockframework:spock-core:1.1-groovy-2.4"
  testRuntimeOnly "cglib:cglib-nodep:3.1"
  testRuntimeOnly "org.objenesis:objenesis:2.1"

  funTestCompile ratpack.dependency("test")
  funTestCompile "io.cucumber:cucumber-junit:6.1.1"
  funTestCompile "io.cucumber:cucumber-groovy:6.1.1"
  funTestCompile "com.github.groovy-wslite:groovy-wslite:1.1.2"
}

mainClassName = "io.sdkman.broker.Main"

run {
  systemProperties System.getProperties()
}

sourceSets {
  funtest {
    groovy.srcDir file("src/funtest/groovy")
    resources.srcDir     file("src/funtest/resources")
    compileClasspath = sourceSets.main.output + sourceSets.test.output + configurations.funTestCompile
    runtimeClasspath = output + compileClasspath + configurations.funTestRuntime
  }
}

assemble.dependsOn shadowJar

task funtest(type: Test) {
  testClassesDirs = sourceSets.funtest.output.classesDirs
  classpath = sourceSets.funtest.runtimeClasspath
}

check.dependsOn funtest
