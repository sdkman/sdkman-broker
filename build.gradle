plugins {
  id "groovy"
  id "org.jetbrains.kotlin.jvm" version "1.7.21"
  id "io.ratpack.ratpack-java" version "1.9.0"
  id "com.github.johnrengelman.shadow" version "8.1.1"
  id "com.netflix.nebula.release" version "19.0.10"
}

repositories {
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

configurations {
  funTestCompile.extendsFrom testImplementation
  funTestRuntime.extendsFrom testRuntimeOnly
}

dependencies {
  runtimeOnly "org.slf4j:slf4j-simple:2.0.17"

  implementation "io.arrow-kt:arrow-core:1.0.1"
  implementation ratpack.dependency("guice")
  implementation "org.mongodb:mongo-java-driver:3.2.2"
  implementation "org.slf4j:slf4j-simple:1.7.5"
  implementation "com.github.sdkman:sdkman-persistent-model:1.1.1"

  testImplementation 'org.codehaus.groovy:groovy-all:3.0.7'
  testImplementation "org.spockframework:spock-core:2.3-groovy-3.0"
  testRuntimeOnly "cglib:cglib-nodep:3.1"
  testRuntimeOnly "org.objenesis:objenesis:2.1"

  funTestCompile ratpack.dependency("test")
  funTestCompile "io.cucumber:cucumber-junit:6.1.1"
  funTestCompile "io.cucumber:cucumber-groovy:6.1.1"
  funTestCompile "com.github.groovy-wslite:groovy-wslite:1.1.2"
}

mainClassName = "io.sdkman.broker.Main"

run {
  systemProperties System.getProperties()
}

sourceSets {
  funtest {
    groovy.srcDir file("src/funtest/groovy")
    resources.srcDir     file("src/funtest/resources")
    compileClasspath = sourceSets.main.output + sourceSets.test.output + configurations.funTestCompile
    runtimeClasspath = output + compileClasspath + configurations.funTestRuntime
  }
}

assemble.dependsOn shadowJar

tasks.register("funtest", Test) {
  testClassesDirs = sourceSets.funtest.output.classesDirs
  classpath = sourceSets.funtest.runtimeClasspath
}

check.dependsOn funtest
